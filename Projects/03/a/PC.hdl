// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // register load needed for load,inc,reset:
    Or(a=load, b=reset, out=lrOr);
    Or(a=inc, b=lrOr, out=setLoad);

    // increment or loop channel
    Inc16(in=outLoop, out=outInc);
    Mux16(a=outLoop, b=outInc, sel=inc, out=muxInc);

    // flat input channels
    Mux16(a=muxInc, b=in, sel=load, out=muxLoadOrInc);
    Mux16(a=muxLoadOrInc, b=false, sel=reset, out=muxReset);

    // feed register
    Register(in=muxReset, load=setLoad, out=out, out=outLoop);
}
